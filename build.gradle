plugins {
    id 'java'
    id 'maven-publish'
}

group = 'fr.swansky'
version = '1.1.2'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation("net.dv8tion:JDA:5.0.0")
    implementation('ch.qos.logback:logback-classic:1.4.12')
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/swansky/sscs")
            credentials {
                username = project.findProperty("github.name") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("github.token") ?: System.getenv("PUB_GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenPublish(MavenPublication) {
            from(components.java)
            pom {
                name = 'Simple Slash Command System'
                description = 'A library to implement slash commands for Discord bots using annotations and parameter auto-injection.'
                url = 'https://github.com/Swansky/SSCS'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'Swansky'
                        name = 'Swansky'
                        email = 'pro@swansky.fr'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Swansky/SSCS.git'
                    developerConnection = 'scm:git:ssh://github.com/Swansky/SSCS.git'
                    url = 'https://github.com/swansky/sscs'
                }
            }
        }
    }
}

