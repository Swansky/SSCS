plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.14.0'
}

group = 'fr.swansky'
version = '1.1.0'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation("net.dv8tion:JDA:5.0.0")
    implementation('ch.qos.logback:logback-classic:1.4.12')
}

test {
    useJUnitPlatform()
}


jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {

        maven {
            /*nexus2 {
                'maven-central' {
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local'
                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }*/
            //Portal Publisher API
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }

        }
    }
}

publishing {
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = 'fr.swansky'
            artifactId = 'sscs'

            from components.java
            pom {
                name = 'Simple Slash Command System'
                description = 'A library to implement slash commands for Discord bots using annotations and parameter auto-injection.'
                url = 'https://github.com/Swansky/SSCS'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'Swansky'
                        name = 'Swansky'
                        email = 'pro@swansky.fr'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/Swansky/SSCS.git'
                    developerConnection = 'scm:git:ssh://github.com/Swansky/SSCS.git'
                    url = 'https://github.com/swansky/SSCS'
                }
            }
        }
    }
}

